buildscript {
	ext.kotlin_version = "1.3.72"
	repositories {
		maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
		maven { url "https://plugins.gradle.org/m2/" }
		mavenCentral()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
		classpath "com.moowork.gradle:gradle-node-plugin:1.3.1"
	}
}

apply plugin:"kotlin2js"
apply plugin:"kotlin-dce-js"
apply plugin:"com.moowork.node"

repositories {
	maven { url "http://dl.bintray.com/kotlin/kotlin-eap" }
	mavenCentral()
}

dependencies {
	implementation "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
	testImplementation "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
}

compileKotlin2Js {
	kotlinOptions {
		moduleKind = "umd"
		sourceMap = true
		apiVersion = "1.3"
		languageVersion = "1.3"
	}
}

sourceSets {
	main {
		kotlin { srcDir { "src" } }
	}
	test {
		kotlin { srcDir { "test" } }
	}
}

task copyCompiledJsFile(type: Copy, dependsOn: compileKotlin2Js) {
	from(compileKotlin2Js.destinationDir.absolutePath + "/dependencies")
	from(compileKotlin2Js.destinationDir) {
		include "*.js"
		exclude "*.meta.js"
	}
	into "html/js"
}
build.dependsOn copyCompiledJsFile

[compileKotlin2Js, compileTestKotlin2Js]*.configure {
	kotlinOptions.moduleKind = "umd"
}
task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
	from compileKotlin2Js.destinationDir
	configurations.testCompile.each {
		from zipTree(it.absolutePath).matching { include "*.js" }
	}
	into "$buildDir/node_modules"
}
node {
	download = true
}
task installNpmDependencies(type: NpmTask) {
	args = ["install"]
}
task runJasmine(type: NodeTask, dependsOn: [compileTestKotlin2Js, populateNodeModules, installNpmDependencies]) {
	script = file("node_modules/jasmine/bin/jasmine.js")
	args = [compileTestKotlin2Js.outputFile]
}
test.dependsOn runJasmine